buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
compileJava.options.encoding = 'UTF-8'

// Link properties file (variables now accessible as config.var)
ext.configFile = file "gradle.properties"
configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = config.minecraft_version + "-" + config.mod_version
group = "ca.wescook.wateringcans"
archivesBaseName = "WateringCans"

minecraft {
	version = config.minecraft_version + "-" + config.forge_version
	runDir = "run"
	mappings = config.mappings

	// Replace @VERSION@ in WateringCans.java with mod version from gradle.properties
	replace '@VERSION@', config.mod_version
	replaceIn 'WateringCans.java'
}

repositories {
	maven {
		url "http://dvs1.progwml6.com/files/maven" // JEI
	}
}

dependencies {
	compile "mezz.jei:jei_1.10.2:3.12.8.323:api" // JEI
}

processResources
{
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, that's not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
